import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'multiloader-loader'
    id 'fabric-loom' version '1.7-SNAPSHOT'
}

repositories {
    maven { url 'https://maven.mrcrayfish.com/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation files('libs/framework-fabric-1.21.1-0.13.11-signed.jar')
    modCompileOnly "dev.architectury:architectury-fabric:${architectury_api_version}"
    modCompileOnly "mezz.jei:jei-${jei_minecraft_version}-fabric:${jei_version}"
    modCompileOnly "dev.emi:emi-fabric:${emi_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"
    library files('libs/controllable-sdl-2.32.10-1.1.0.jar')
    implementation "com.google.code.findbugs:jsr305:3.0.1"
}

sourceSets.main.resources.srcDir 'src/generated/resources'

fabricApi {
    configureDataGeneration {
        outputDirectory.set(file("src/generated/resources"))
    }
}

loom {
    def aw = file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

remapJar {
    addNestedDependencies = false
}

task releaseJar(type: RemapJarTask) {
    dependsOn tasks.jar
    input = tasks.jar.archiveFile
    addNestedDependencies = true
    archiveClassifier = 'release'
}
build.finalizedBy(releaseJar)