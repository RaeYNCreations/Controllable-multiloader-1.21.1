import net.neoforged.gradle.common.tasks.PotentiallySignJar

plugins {
    id 'multiloader-loader'
    id 'net.neoforged.gradle.userdev' version '7.0.167'
}

def at = file('src/main/resources/META-INF/accesstransformer.cfg')
if (at.exists()) {
    minecraft.accessTransformers.file at
}

jarJar.enable()

sourceSets {
    test {
        compileClasspath += project(':common').sourceSets.main.output
        runtimeClasspath += project(':common').sourceSets.main.output
    }
}

configurations {
    testImplementation.extendsFrom implementation
    testRuntimeOnly.extendsFrom runtimeOnly
}

runs {
    configureEach {
        modSource project.sourceSets.main
    }
    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }
    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }
    gameTestServer {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }
    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
    junit {

    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    // Core dependencies
    implementation "net.neoforged:neoforge:${neoforge_version}"
    implementation files('libs/framework-neoforge-1.21.1-0.13.11-signed.jar')

    // JEI, EMI, REI...
    compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
    compileOnly "dev.emi:emi-neoforge:${emi_version}"
    compileOnly "me.shedaniel:RoughlyEnoughItems-neoforge:${rei_version}"

    // Controllable SDL
    implementation files('libs/controllable-sdl-2.32.10-1.1.0.jar')
    jarJar(files('libs/controllable-sdl-2.32.10-1.1.0.jar'))

    testImplementation "org.junit.jupiter:junit-jupiter:5.7.2"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

// Custom task to manually bundle SDL into the jar
tasks.register('bundleSDL', Jar) {
    dependsOn jar
    archiveClassifier = 'release'
    from zipTree(jar.archiveFile)
    from(zipTree('libs/controllable-sdl-2.32.10-1.1.0.jar'))
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    from(rootProject.file("LICENSE_LibSDL4J"))
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }
    
    manifest {
        from jar.manifest
    }
}

build.dependsOn bundleSDL

tasks.register('signJar', PotentiallySignJar) {
    dependsOn bundleSDL
    onlyIf {
        hasProperty('keyStore') || System.getenv("KEYSTORE")
    }
    keyStore = findProperty('keyStore') ?: System.getenv("KEYSTORE")
    alias = findProperty('keyStoreAlias') ?: System.getenv("KEYSTORE_ALIAS")
    storePass = findProperty('keyStorePass') ?: System.getenv("KEYSTORE_PASS")
    input = bundleSDL.archiveFile
}

jar.finalizedBy bundleSDL
bundleSDL.finalizedBy 'signJar'

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

test {
    useJUnitPlatform()
}